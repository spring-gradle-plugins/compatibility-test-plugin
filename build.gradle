plugins {
	id "checkstyle"
	id "io.spring.javaformat" version "$springJavaFormatVersion"
	id "io.spring.nohttp" version "0.0.11"
	id "java-gradle-plugin"
	id "maven-publish"
}

group "io.spring.gradle"
description = "Compatibility Test Plugin"

gradlePlugin {
	plugins {
		compatibilityTest {
			description = project.description
			displayName = project.description
			id = "io.spring.compatibility-test"
			implementationClass = "io.spring.gradle.compatibilitytest.CompatibilityTestPlugin"
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	checkstyle("io.spring.javaformat:spring-javaformat-checkstyle:$springJavaFormatVersion")
	testImplementation("org.assertj:assertj-core:3.25.3")
	testImplementation("org.junit.jupiter:junit-jupiter:5.10.2")
	testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	withJavadocJar()
	withSourcesJar()
}

test {
	useJUnitPlatform()
}

publishing {
	publications.withType(MavenPublication) {
		pom {
			name = project.description
			description = project.description
			url = "https://github.com/spring-gradle-plugins/compatibility-test-plugin"
			organization {
				name = "Spring IO"
				url = "https://spring.io"
			}
			licenses {
				license {
					name = "The Apache Software License, Version 2.0"
					url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
					distribution = "repo"
				}
			}
			scm {
				url = "https://github.com/spring-gradle-plugins/compatibility-test-plugin"
				connection = "scm:git:git://github.com/spring-gradle-plugins/compatibility-test-plugin"
				developerConnection = "scm:git:git://github.com/spring-gradle-plugins/compatibility-test-plugin"
			}
			developers {
				developer {
					id = "wilkinsona"
					name = "Andy Wilkinson"
					email = "wilkinsona@vmware.com"
				}
			}
			issueManagement {
				system = "GitHub"
				url = "https://github.com/spring-gradle-plugins/compatibility-test-plugin"
			}
		}
	}
	if (project.hasProperty("distributionRepository")) {
		repositories {
			maven {
				url = "${distributionRepository}"
				name = "distribution"
			}
		}
	}
}
